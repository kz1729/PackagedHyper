plugins {
	id "com.crystaelix.loom" version "1.9.0"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.crystaelix.com/releases/"
			}
		}
		filter {
			includeGroup "thelm.packagedauto"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "appeng"
			includeGroup "mekanism"
		}
	}
}

sourceSets {
	main
}

def mcVersion = "1.19.2"
def modName = "PackagedAvaritia-Universal"
def modId = "packagedavaritia"
def specVersion = "2"
def vendor = "thelm"

group = "thelm.packagedavaritia"
version = "2.1.0.0"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
		}
		client
		server
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.24.1"

	// Minecraft
	minecraft "com.mojang:minecraft:1.19.2"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-1.19.2:2022.11.27@zip"
	}
	forge "net.minecraftforge:forge:1.19.2-43.4.2"

	// Dependencies
	modImplementation "thelm.packagedauto:PackagedAuto-1.19.2:3.2.14.45"
	modImplementation "curse.maven:avaritia-universal-891434:5506589"
	modImplementation("mezz.jei:jei-1.19.2-forge:11.8.1.1034") {transitive = false}
	modImplementation "appeng:appliedenergistics2-forge:12.9.12"
	modImplementation "vazkii.patchouli:Patchouli:1.19.2-77"

	// Testing
	modImplementation "mekanism:Mekanism:1.19.2-10.3.9.13"
	modImplementation "curse.maven:applied-mekanistics-574300:4734608"

	// Performance
	modImplementation "curse.maven:yeetusexperimentus-635427:3909789"
	implementation "io.github.llamalad7:mixinextras-forge:0.3.6"
	modImplementation "curse.maven:modernfix-790626:5803581"
	modImplementation "curse.maven:ferritecore-429235:4117906"
	modImplementation "curse.maven:lazy-dfu-forge-460819:4327266"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml", "META-INF/neoforge.mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

publishing {
	publications {
		register("mavenJava", MavenPublication) {
			artifact remapJar
			artifact sourcesJar
		}
	}
	repositories {
		if(project.hasProperty("CrystaelixMavenUsername") && project.hasProperty("CrystaelixMavenPassword")) {
			maven {
				name = "CrystaelixMaven"
				url = "https://maven.crystaelix.com/releases/"
				credentials(PasswordCredentials)
			}
		}
	}
}

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-Re-${project.version}"
	changelog = file("changelog.md").text
	type = BETA
	modLoaders.add("forge")
	additionalFiles.from(sourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "308383"
		minecraftVersions.add(mcVersion)
		requires "packagedauto", "re-avaritia"
		optional "applied-energistics-2"
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "aA94MQL4"
		minecraftVersions.add(mcVersion)
		requires "packagedauto", "re-avaritia"
		optional "ae2"
	}
}
