plugins {
	id "com.crystaelix.loom" version "1.11-SNAPSHOT"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "com.crystaelix.blossom" version "1.3.2"
	id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

repositories {
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.parchmentmc.net/"
			}
		}
		filter {
			includeGroup "org.parchmentmc.data"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.crystaelix.com/releases/"
			}
		}
		filter {
			includeGroup "thelm.packagedauto"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
			includeGroup "vazkii.patchouli"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://modmaven.dev/"
			}
		}
		filter {
			includeGroup "appeng"
			includeGroup "mekanism"
		}
	}
}

def mcVersion = "1.20.1"
def modName = "PackagedAvaritia-Neo"
def modId = "packagedavaritia"
def specVersion = "2"
def vendor = "thelm"

group = "thelm.packagedavaritia"
version = "2.2.1.1"

def archivesName = "${modName}-${mcVersion}"
base.archivesName = archivesName

sourceSets {
	main
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
		vendor = JvmVendorSpec.AZUL
	}
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
		}
		client
		server
	}
}

dependencies {
	// Log4j
	implementation "org.apache.logging.log4j:log4j-core:2.24.3"

	// Minecraft
	minecraft "com.mojang:minecraft:1.20.1"
	mappings loom.layered {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip"
	}
	forge "net.minecraftforge:forge:1.20.1-47.4.4"

	// Dependencies
	modImplementation "thelm.packagedauto:PackagedAuto-1.20.1:3.4.14.44"
	modImplementation "curse.maven:avaritianeo-1083158:6824388"
	modImplementation("mezz.jei:jei-1.20.1-forge:15.20.0.112") {transitive = false}
	modImplementation "org.appliedenergistics:guideme:20.1.11"
	modImplementation "appeng:appliedenergistics2-forge:15.4.6"
	modImplementation "vazkii.patchouli:Patchouli:1.20.1-84.1-FORGE"

	// Testing
	modImplementation "mekanism:Mekanism:1.20.1-10.4.16.80"
	modImplementation "curse.maven:applied-mekanistics-574300:4842281"

	// Performance
	modImplementation "curse.maven:yeetusexperimentus-635427:4680832"
	implementation "io.github.llamalad7:mixinextras-forge:0.4.1"
	modImplementation "curse.maven:modernfix-790626:6766127"
	modImplementation "curse.maven:ferritecore-429235:4810975"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml", "META-INF/neoforge.mods.toml", "fabric.mod.json"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

publishing {
	publications {
		register("mavenJava", MavenPublication) {
			artifactId = archivesName
			artifact remapJar
			artifact sourcesJar
		}
	}
	repositories {
		if(project.hasProperty("CrystaelixMavenUsername") && project.hasProperty("CrystaelixMavenPassword")) {
			maven {
				name = "CrystaelixMaven"
				url = "https://maven.crystaelix.com/releases/"
				credentials(PasswordCredentials)
			}
		}
	}
}

publishMods {
	file = remapJar.archiveFile
	version = displayName = "${mcVersion}-Neo-${project.version}"
	changelog = file("changelog.md").text
	type = BETA
	modLoaders.addAll("forge", "neoforge")
	additionalFiles.from(sourcesJar.archiveFile)

	curseforge {
		accessToken = providers.gradleProperty("curseforge.token")
		projectId = "308383"
		minecraftVersions.add(mcVersion)
		requires "packagedauto", "avaritianeo"
		optional "applied-energistics-2"
		incompatible "avaritia-universal", "re-avaritia", "avaritiareborn", "avaritia-lite", "avaritia-continued", "avaritia-reset-version"
	}

	modrinth {
		accessToken = providers.gradleProperty("modrinth.token")
		projectId = "aA94MQL4"
		minecraftVersions.add(mcVersion)
		requires "packagedauto", "avaritianeo"
		optional "ae2"
		incompatible "re-avaritia", "neo-avaritia"
	}
}
